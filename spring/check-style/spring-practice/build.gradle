plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id "org.ec4j.editorconfig" version "0.0.3"
    id 'checkstyle'
    id 'pmd'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

editorconfig {
    // Exlude some files in addition to the default excludes
    // Paths or patterns must be relative to the the project root directory.
    excludes = ['build']
}

checkstyle {
    maxWarnings = 0
    configFile = file("${rootDir}/config/checkstyle/google-checkstyle-indent4.xml")
    configProperties = ["suppressionFile": "${rootDir}/config/checkstyle/checkstyle-suppressions.xml"]
    toolVersion = "10.3.2"
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

check.dependsOn editorconfigCheck

pmd {
    consoleOutput = true
    toolVersion = "6.49.0"
    rulesMinimumPriority = 5
    // https://pmd.sourceforge.io/pmd-6.39.0/pmd_userdocs_making_rulesets.html custom ruleset 만들기.
    ruleSetFiles = files("config/pmd/custom-pmd-basic-ruleset.xml")
    pmdTest.enabled = true
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
